/*
 * Copyright 2015 Rainer Schuster
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
/*
 * generated by Xtext
 */
package com.rainerschuster.webidl.generator

import com.google.inject.Inject
import com.rainerschuster.webidl.webIDL.Argument
import com.rainerschuster.webidl.webIDL.Attribute
import com.rainerschuster.webidl.webIDL.CallbackFunction
import com.rainerschuster.webidl.webIDL.Const
import com.rainerschuster.webidl.webIDL.ExtendedAttributeList
import com.rainerschuster.webidl.webIDL.ExtendedInterfaceMember
import com.rainerschuster.webidl.webIDL.Interface
import com.rainerschuster.webidl.webIDL.InterfaceMember
import com.rainerschuster.webidl.webIDL.Operation
import com.rainerschuster.webidl.webIDL.Special
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.IFileSystemAccess
import org.eclipse.xtext.generator.IGenerator
import org.eclipse.xtext.naming.IQualifiedNameProvider

import static extension com.rainerschuster.webidl.util.NameUtil.*
import static extension com.rainerschuster.webidl.util.TypeUtil.*
import com.rainerschuster.webidl.webIDL.ImplementsStatement
import com.google.common.collect.ArrayListMultimap
import com.rainerschuster.webidl.webIDL.Typedef
import com.google.common.collect.ListMultimap
import java.util.List

/**
 * Generates code from your model files on save.
 * 
 * see http://www.eclipse.org/Xtext/documentation.html#TutorialCodeGeneration
 */
class WebIDLGenerator implements IGenerator {

	@Inject extension IQualifiedNameProvider

	override void doGenerate(Resource resource, IFileSystemAccess fsa) {
		var ListMultimap<Interface, Interface> implementsMap = ArrayListMultimap.create();
		for (e : resource.allContents.toIterable.filter(typeof(ImplementsStatement))) {
			// TODO resolveInterfaceOrTypedef
			val ifaceB = switch (e.ifaceB) {
				Interface: e.ifaceB as Interface
				Typedef: resolveDefinition(e.ifaceB as Typedef) as Interface
			};
			implementsMap.put(e.ifaceA, ifaceB);
		};
		for (e : resource.allContents.toIterable.filter(typeof(Interface))) {
			val allExtends = newArrayList();
			if (e.inherits != null) {
				// TODO resolveInterfaceOrTypedef
				val inherits = switch (e.inherits) {
					Interface: e.inherits as Interface
					Typedef: resolveDefinition(e.inherits as Typedef) as Interface
				};
				allExtends.add(inherits);
			}
			if (implementsMap.containsKey(e)) {
				allExtends.addAll(implementsMap.get(e));
			}
			fsa.generateFile(e.fullyQualifiedName.toString("/") + ".java", e.binding(allExtends));
		};
		for (e : resource.allContents.toIterable.filter(typeof(CallbackFunction))) {
			fsa.generateFile(e.fullyQualifiedName.toString("/") + ".java", e.binding);
		};
	}

	// TODO consider eCrossReferences for implementsStatements!

	def binding(Interface iface, List<Interface> allExtends) '''
		«IF iface.eContainer.fullyQualifiedName != null»
			package «iface.eContainer.fullyQualifiedName»;

		«ENDIF»

		public interface «iface.name»«IF !allExtends.nullOrEmpty» extends «FOR i : allExtends SEPARATOR ', '»«i.fullyQualifiedName»«ENDFOR»«ENDIF» {
		«FOR i : iface.interfaceMembers SEPARATOR '\n'»
			«binding(i)»
		«ENDFOR»
		}
	'''

	def binding(CallbackFunction callback) '''
		«IF callback.eContainer.fullyQualifiedName != null»
			package «callback.eContainer.fullyQualifiedName»;

		«ENDIF»

		public interface «callback.name» {
			«callback.type.toJavaType» call(«FOR i : callback.arguments SEPARATOR ', '»«binding(i)»«ENDFOR»);
		}
	'''

	def binding(ExtendedInterfaceMember member) {
		bindingInterfaceMember(member.eal, member.interfaceMember)
	}

	def dispatch bindingInterfaceMember(ExtendedAttributeList eal, InterfaceMember interfaceMember) {
		System.out.println("Fallback method - Unsupported type " + interfaceMember.class.name + "!");
	}

	/* TODO NON-SPEC: Added "public static final " */
	def dispatch bindingInterfaceMember(ExtendedAttributeList eal, Const constant) '''
		«constant.type.toJavaType» «constant.name.getEscapedJavaName» = «constant.constValue»;

	'''

	// TODO is... for boolean! (non-nullable?!)
	def dispatch bindingInterfaceMember(ExtendedAttributeList eal, Attribute attribute) '''
		«IF !attribute.inherit»
			«attribute.type.toJavaType» get«attribute.name.toFirstUpper»();
		«ENDIF»
		«IF !attribute.readOnly»
			void set«attribute.name.toFirstUpper»(«attribute.type.toJavaType» «attribute.name.getEscapedJavaName»);
		«ENDIF»

	'''

	// FIXME What if more than one specials occur, e.g.: setter creator void (unsigned long index, HTMLOptionElement? option);
	def dispatch bindingInterfaceMember(ExtendedAttributeList eal, Operation operation) '''
		«operation.type.toJavaType» «IF operation.name.nullOrEmpty»«IF operation.specials.contains(Special.GETTER)»_get«ELSEIF operation.specials.contains(Special.SETTER)»_set«ELSEIF operation.specials.contains(Special.CREATOR)»_create«ELSEIF operation.specials.contains(Special.DELETER)»_delete«ELSEIF operation.specials.contains(Special.LEGACYCALLER)»_call«ENDIF»«ELSE»«operation.name.getEscapedJavaName»«ENDIF»(«FOR i : operation.arguments SEPARATOR ', '»«binding(i)»«ENDFOR»);
	'''

	def binding(Argument parameter) '''
		«parameter.type.toJavaType»«IF parameter.ellipsis»...«ENDIF» «parameter.name.getEscapedJavaName»'''

}
